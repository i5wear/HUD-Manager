plugins {
    id 'dev.architectury.loom' version '1.11-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT' apply false
    id 'com.gradleup.shadow' version '8.3.6' apply false
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'com.gradleup.shadow'

    architectury.minecraft = minecraft_version
    project.group = maven_group
    project.version = mod_version

    repositories {
        maven { url = 'https://maven.neoforged.net/releases' }
        maven { url = "https://maven.terraformersmc.com/releases" }
        maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" }
    }

    tasks.withType(JavaCompile).configureEach {
        options.release = 21
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    processResources {
        filesMatching('fabric.mod.json') { expand version: mod_version }
        filesMatching('neoforge.mods.toml') { expand version: mod_version }
    }
}